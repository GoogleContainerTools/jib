plugins {
  id 'io.freefair.maven-plugin'
  id 'net.researchgate.release'
  id 'maven-publish'
  id 'eclipse'
}

// only maven specific dependencies should be versioned, everything else should be defined by constrains in
// parent build.gradle
dependencies {
  sourceProject project(':jib-core')
  sourceProject project(':jib-plugins-common')
  ensureNoProjectDependencies()

  implementation dependencyStrings.MAVEN_EXTENSION

  implementation dependencyStrings.MAVEN_API
  implementation dependencyStrings.MAVEN_API

  // compileOnly + testImplementation equivalent to "provided"
  compileOnly 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.6.0'
  // needed to suppress "unknown enum constant" warnings
  testImplementation 'org.apache.maven.plugin-tools:maven-plugin-annotations:3.5'

  // maven-plugin-testing-harness pulls in conflicting implementations of DefaultPlexusContainer
  // (sisu (correct) vs default-plexus-container (wrong)) so ensure this is first in the test classpath
  testImplementation 'org.eclipse.sisu:org.eclipse.sisu.plexus:0.3.4'
  testImplementation 'org.apache.maven.plugin-testing:maven-plugin-testing-harness:3.3.0'

  testImplementation dependencyStrings.JUNIT
  testImplementation dependencyStrings.MOCKITO_CORE
  testImplementation dependencyStrings.SLF4J_API
  testImplementation dependencyStrings.SYSTEM_RULES

  testImplementation 'org.apache.maven.shared:maven-verifier:1.7.2'
  testImplementation dependencyStrings.MAVEN_API
  testImplementation 'org.slf4j:slf4j-api:1.7.30'
  testImplementation 'org.slf4j:slf4j-simple:1.7.30'

  integrationTestImplementation dependencyStrings.JBCRYPT

  testImplementation project(path:':jib-plugins-common', configuration:'tests')
  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')
}

/* TESTING */
test.dependsOn publishToMavenLocal
integrationTest.dependsOn publishToMavenLocal
/* TESTING */

/* RELEASE */
configureMavenRelease()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Jib'
        description = 'A Maven plugin for building container images for your Java applications.'
      }
    }
  }
}


release {
  tagTemplate = 'v$version-maven'
  ignoredSnapshotDependencies = [
    'com.google.cloud.tools:jib-core',
    'com.google.cloud.tools:jib-plugins-common',
  ]
  git {
    requireBranch = /^maven_release_v\d+.*$/  //regex
  }
}
/* RELEASE */

/* ECLIPSE */
eclipse.classpath.file.whenMerged {
  entries.each {
    if (it.path == 'src/test/resources') {
      it.excludes += 'maven/projects/'
    }
    // TODO: remove after upgrading to Gradle 5.6. (https://github.com/eclipse/buildship/issues/689)
    if (it.path in ['src/test/java', 'src/test/resources',
        'src/integration-test/java', 'src/integration-test/resources']) {
      it.entryAttributes['test'] = 'true'
    }
  }
}
/* ECLIPSE */
