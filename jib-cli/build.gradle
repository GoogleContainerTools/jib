plugins {
  id 'application'
  id 'net.researchgate.release'
  id 'eclipse'
}

ext {
  cliMainClass = 'com.google.cloud.tools.jib.cli.JibCli'
}

// use `run` to build and run the app
// use `installDist` or `distZip` to create an installable application
application {
  applicationName = 'jib'
  mainClassName = cliMainClass
}

dependencies {
  implementation project(':jib-core')
  implementation project(':jib-plugins-common')

  implementation dependencyStrings.COMMONS_TEXT
  implementation dependencyStrings.JACKSON_DATAFORMAT_YAML
  implementation dependencyStrings.JACKSON_DATABIND
  implementation dependencyStrings.GUAVA
  implementation dependencyStrings.PICOCLI

  testImplementation dependencyStrings.JUNIT
  testImplementation dependencyStrings.JUNIT_PARAMS
  testImplementation dependencyStrings.TRUTH
  testImplementation dependencyStrings.TRUTH8
  testImplementation dependencyStrings.MOCKITO_CORE
  testImplementation dependencyStrings.SLF4J_API
  testImplementation dependencyStrings.SYSTEM_RULES

  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')
  integrationTestImplementation gradleTestKit()
}

release {
  tagTemplate = 'v$version-cli'
  ignoredSnapshotDependencies = [
    'com.google.cloud.tools:jib-core',
    'com.google.cloud.tools:jib-plugins-common',
  ]
  git {
    requireBranch = /^cli-release-v\d+.*$/  //regex
  }
}

/* ECLIPSE */
// TODO: remove after upgrading to Gradle 5.6. (https://github.com/eclipse/buildship/issues/689)
// Also remove the 'eclipse' plugin.
eclipse.classpath.file.whenMerged {
  entries.findAll {
    it.path in ['src/test/java', 'src/test/resources', 'test-resources']
  }.each {
    it.entryAttributes['test'] = 'true'
  }
}
/* ECLIPSE */
