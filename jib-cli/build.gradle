plugins {
  id 'application'
  id 'eclipse'
}

ext {
  cliMainClass = 'com.google.cloud.tools.jib.cli.JibCli'
}

// use `run` to build and run the app
// use `installDist` or `distZip` to create an installable application
application {
  applicationName = 'jib'
  mainClassName = cliMainClass
}

dependencies {
  implementation project(':jib-core')
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${dependencyVersions.JACKSON_DATAFORMAT_YAML}"
  implementation "com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.JACKSON_DATABIND}"
  implementation "com.google.guava:guava:${dependencyVersions.GUAVA}"
  implementation "info.picocli:picocli:${dependencyVersions.PICOCLI}"

  testImplementation "junit:junit:${dependencyVersions.JUNIT}"
  testImplementation "org.mockito:mockito-core:${dependencyVersions.MOCKITO_CORE}"
  testImplementation "org.slf4j:slf4j-api:${dependencyVersions.SLF4J_API}"
  testImplementation "com.github.stefanbirkner:system-rules:${dependencyVersions.SYSTEM_RULES}"
}

/* ECLIPSE */
// TODO: remove after upgrading to Gradle 5.6. (https://github.com/eclipse/buildship/issues/689)
// Also remove the 'eclipse' plugin.
eclipse.classpath.file.whenMerged {
  entries.findAll {
    it.path in ['src/test/java', 'src/test/resources', 'test-resources']
  }.each {
    it.entryAttributes['test'] = 'true'
  }
}
/* ECLIPSE */
