plugins {
  id 'java-gradle-plugin'
  id 'application'
  id 'eclipse'
}

ext {
  cliMainClass = 'com.google.cloud.tools.jib.cli.JibCli'
}

// use `run` to build and run the app
// use `installDist` or `distZip` to create an installable application
application {
  applicationName = 'jib'
  mainClassName = cliMainClass
}

dependencies {
  implementation project(':jib-core')
  implementation project(':jib-plugins-common')

  implementation dependencyStrings.GRADLE_EXTENSION

  implementation dependencyStrings.COMMONS_TEXT
  implementation dependencyStrings.JACKSON_DATAFORMAT_YAML
  implementation dependencyStrings.JACKSON_DATABIND
  implementation dependencyStrings.GUAVA
  implementation dependencyStrings.PICOCLI

  testImplementation dependencyStrings.JUNIT
  testImplementation dependencyStrings.JUNIT_PARAMS
  testImplementation dependencyStrings.TRUTH
  testImplementation dependencyStrings.TRUTH8
  testImplementation dependencyStrings.MOCKITO_CORE
  testImplementation dependencyStrings.SLF4J_API
  testImplementation dependencyStrings.SYSTEM_RULES

  // only for testing a concrete Spring Boot example in a test (not for test infrastructure)
  testImplementation 'org.springframework.boot:spring-boot-gradle-plugin:2.2.11.RELEASE'

  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')
}

/* ECLIPSE */
// TODO: remove after upgrading to Gradle 5.6. (https://github.com/eclipse/buildship/issues/689)
// Also remove the 'eclipse' plugin.
eclipse.classpath.file.whenMerged {
  entries.findAll {
    it.path in ['src/test/java', 'src/test/resources', 'test-resources']
  }.each {
    it.entryAttributes['test'] = 'true'
  }
}
/* ECLIPSE */
