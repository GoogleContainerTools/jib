plugins {
    id 'java-gradle-plugin'
    id 'checkstyle'
    id 'com.github.sherter.google-java-format' version '0.6'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'maven'
    id 'net.ltgt.apt' version '0.13'
    id 'net.ltgt.errorprone' version '0.0.13'
}

group 'com.google.cloud.tools'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Use jcenter for gradle plugin portal releases.
    jcenter()
}

sourceSets {
    main.java.srcDir file('../jib-core/src/main/java')
    test.java.srcDir file('../jib-core/src/test/java')
    test.resources.srcDir file('../jib-core/src/test/resources')

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    // These are copied over from jib-core and are necessary for the jib-core sourcesets.
    compile 'com.google.http-client:google-http-client:1.23.0'
    compile 'org.apache.commons:commons-compress:1.15'
    compile 'com.google.guava:guava:23.5-jre'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.2'
    compile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.12.0'

    compile gradleApi()

    // NullAway errorprone plugin
    // TODO: Update to latest version once https://github.com/uber/NullAway/issues/133 is fixed.
    apt 'com.uber.nullaway:nullaway:0.3.2'
    errorprone 'com.google.errorprone:error_prone_core:2.2.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.3.1"
}

// Integration tests must be run explicitly
task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
integrationTest.dependsOn test

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

// Adds NullAway errorprone checks.
tasks.withType(JavaCompile) {
    if (!name.toLowerCase().contains("test")) {
        options.compilerArgs += ["-Xep:NullAway:ERROR", "-XepOpt:NullAway:AnnotatedPackages=com.google.cloud.tools"]
    }
}

tasks.withType(Test) {
    reports.html.setDestination file("${reporting.baseDir}/${name}")
}

/* GOOGLE JAVA FORMAT */
googleJavaFormat {
    toolVersion = '1.5'
}
check.dependsOn verifyGoogleJavaFormat
/* GOOGLE JAVA FORMAT */

/* CHECKSTYLE */
checkstyle {
    toolVersion = "7.6.1"

    // get the google_checks.xml file from the checkstyle jar and take out the java checks
    def googleChecks = resources.text.fromArchiveEntry(configurations.checkstyle[0], 'google_checks.xml').asString()
    def fileExtensionsBefore = '<property name="fileExtensions" value="java, properties, xml"/>'
    def fileExtensionsAfter = '<property name="fileExtensions" value="properties, xml"/>'
    def googleChecksNoJava = googleChecks.replace(fileExtensionsBefore, fileExtensionsAfter)
    assert !googleChecks.equals(googleChecksNoJava)

    config = resources.text.fromString(googleChecksNoJava)

    maxErrors = 0
    maxWarnings = 0
}
/* CHECKSTYLE */

/* RELEASE */
// gradle plugin portal releases
pluginBundle {
    website = 'https://github.com/google/jib/'
    vcsUrl = 'https://github.com/google/jib/'

    plugins {
        minikubePlugin {
            id = 'com.google.cloud.tools'
            displayName = 'Jib'
            description = 'Containerize your Java application'
            tags = ['google', 'java', 'containers', 'docker', 'kubernetes', 'microservices']
        }
    }
}
/* RELEASE */
