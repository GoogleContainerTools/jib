plugins {
  id 'java-gradle-plugin'
  id 'net.researchgate.release'
  id 'com.gradle.plugin-publish'
  // for eclipse import modifications
  id 'eclipse'
}

/* LOCAL SNAPSHOT DEV */
// to install for local testing - do not load this plugin when publishing to plugin portal
// 'maven-publish' and 'com.gradle.plugin-publish' do not interact well with each other
// https://discuss.gradle.org/t/debug-an-issue-in-publish-plugin-gradle-plugin-not-being-prepended-to-groupid/32720
if (version.contains("SNAPSHOT")) {
  apply plugin: 'maven-publish'

  publishing {
    repositories {
      mavenLocal()
    }
  }

  task install {
    dependsOn publishToMavenLocal
  }
}
/* LOCAL SNAPSHOT DEV */

repositories {
  // because gradle plugin dependencies are pulling from jcenter
  jcenter()
}

dependencies {
  sourceProject project(':jib-core')
  sourceProject project(':jib-plugins-common')
  ensureNoProjectDependencies()

  implementation "com.google.cloud.tools:jib-gradle-plugin-extension-api:${dependencyVersions.GRADLE_EXTENSION}"

  testImplementation "junit:junit:${dependencyVersions.JUNIT}"
  testImplementation "org.mockito:mockito-core:${dependencyVersions.MOCKITO_CORE}"
  testImplementation "org.slf4j:slf4j-api:${dependencyVersions.SLF4J_API}"
  testImplementation "com.github.stefanbirkner:system-rules:${dependencyVersions.SYSTEM_RULES}"


  testImplementation project(path:':jib-plugins-common', configuration:'tests')
  integrationTestImplementation project(path:':jib-core', configuration:'integrationTests')

  integrationTestImplementation "org.mindrot:jbcrypt:${dependencyVersions.JBCRYPT}"

  // only for testing a concrete Spring Boot example in a test (not for test infrastructure)
  testImplementation 'org.springframework.boot:spring-boot-gradle-plugin:2.3.1.RELEASE'
}

/* RELEASE */
// Prepare release
release {
  tagTemplate = 'v$version-gradle'
  ignoredSnapshotDependencies = [
    'com.google.cloud.tools:jib-core',
    'com.google.cloud.tools:jib-plugins-common',
    // TODO: remove when depending on published artifacts.
    'com.google.cloud.tools:jib-build-plan',
    'com.google.cloud.tools:jib-plugins-extension-common',
    'com.google.cloud.tools:jib-gradle-plugin-extension-api',
  ]
  git {
    requireBranch = /^gradle_release_v\d+.*$/  //regex
  }
}
// Gradle Plugin Portal releases
pluginBundle {
  website = 'https://github.com/GoogleContainerTools/jib/'
  vcsUrl = 'https://github.com/GoogleContainerTools/jib/'
  tags = ['google', 'java', 'containers', 'docker', 'kubernetes', 'microservices']
}

gradlePlugin {
  plugins {
    jibPlugin {
      id = 'com.google.cloud.tools.jib'
      displayName = 'Jib'
      description = 'Containerize your Java application'
      implementationClass = 'com.google.cloud.tools.jib.gradle.JibPlugin'
    }
  }
}
tasks.publishPlugins.dependsOn integrationTest
/* RELEASE */

/* ECLIPSE */
eclipse.classpath.file.whenMerged {
  entries.each {
    if (it.path == 'src/test/resources' || it.path == 'src/integration-test/resources') {
      it.excludes += 'gradle/projects/'
    }
    // TODO: remove after upgrading to Gradle 5.6. (https://github.com/eclipse/buildship/issues/689)
    if (it.path in ['src/test/java', 'src/test/resources',
        'src/integration-test/java', 'src/integration-test/resources']) {
      it.entryAttributes['test'] = 'true'
    }
  }
}
/* ECLIPSE */
