plugins {
  id 'net.researchgate.release'
  id 'maven-publish'
  id 'eclipse'
}

dependencies {
  api dependencyStrings.BUILD_PLAN
  api dependencyStrings.EXTENSION_COMMON
  api dependencyStrings.MAVEN_CORE
}

jar {
  manifest {
    attributes 'Implementation-Version': archiveVersion
    attributes 'Automatic-Module-Name': 'com.google.cloud.tools.jib.maven.extension'

    // OSGi metadata
    attributes 'Bundle-SymbolicName': 'com.google.cloud.tools.jib.maven.extension'
    attributes 'Bundle-Name': 'Extension API for Jib Maven Plugin'
    attributes 'Bundle-Vendor': 'Google LLC'
    attributes 'Bundle-DocURL': 'https://github.com/GoogleContainerTools/jib'
    attributes 'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0'
    attributes 'Export-Package': 'com.google.cloud.tools.jib.maven.extension'
  }
}

/* RELEASE */
configureMavenRelease()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Extension API for Jib Maven Plugin'
        description = 'Provides API to extend Jib Maven Plugin containerization.'
      }
      from components.java
    }
  }
}

// Release plugin (git release commits and version updates)
release {
  tagTemplate = 'v$version-maven-extension'
  git {
    requireBranch.set(/^maven-extension-release-v\d+.*$/)
  }
}
/* RELEASE */

/* ECLIPSE */
eclipse.classpath.plusConfigurations += [configurations.integrationTestImplementation]
/* ECLIPSE */
